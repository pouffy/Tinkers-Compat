plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'io.github.0ffz.github-packages' version '[1,2)'
    alias libs.plugins.modDevGradle
    alias libs.plugins.machete // automatic jar compressing on build
    //alias libs.plugins.shadow
    //alias libs.plugins.spotless
    alias libs.plugins.lombok
}

group = mod_group_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

// Formats the mod version to include the loader, Minecraft version, build number (if present), and '-SNAPSHOT-<timestamp>' if enabled.
boolean isSnapshot = System.getenv("SNAPSHOT")
version = "${mod_version}" + (isSnapshot ? "-SNAPSHOT" : "")
boolean isCI = System.getenv("CI") != null

sourceSets {
    // Include resources generated by data generators.
    main.resources {
        srcDir 'src/generated/resources'
    }

    test {
        kotlin {
            srcDirs += 'src/test/java'
        }
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }

    extra {
        compileClasspath += main.output
        runtimeClasspath += main.output + main.runtimeClasspath
    }
}

obfuscation {
    createRemappingConfiguration(configurations.extraRuntimeOnly)
}

apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"


generateModMetadata.doFirst {
    mkdir('run/server')
    mkdir('run/gametest')
    mkdir('run/data')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:-removal"
    options.compilerArgs << "-Aquiet=true" // Suppress mixin notes
}

machete {
    // disable machete locally for faster builds
    enabled = isCI
    // Only optimize reobf jars
    ignoredTasks.addAll("jar", "slimJar")
    additionalTasks.addAll("reobfJar", "reobjSlimJar")
}

lombok {
    version = "1.18.36"
}
