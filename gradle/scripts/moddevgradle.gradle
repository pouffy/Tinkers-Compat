import org.slf4j.event.Level

mixin {
    var refmap = add sourceSets.main, "tcompat.refmap.json"
    slimJar.from refmap
    config 'tcompat.mixins.json'
}

legacyForge {
    // Specify Forge version
    version = libs.versions.minecraftForge.get()

    parchment {
        minecraftVersion = libs.versions.minecraft.get()
        mappingsVersion = libs.versions.parchment.get()
    }

    accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    addModdingDependenciesTo(sourceSets.test)
    //addModdingDependenciesTo(sourceSets.extra)

    mods {
        // define mod <-> source bindings
        "${mod_id}" {
            sourceSet(sourceSets.main)
            sourceSet(sourceSets.test)
            sourceSet(sourceSets.extra)
        }
    }

    runs {
        client {
            client()
            sourceSet = sourceSets.extra
            ideName = "Client 1"

            programArguments.addAll('--username', 'PouffyDev_1')
            systemProperty('forge.enabledGameTestNamespaces', "tcompat")
        }

        client2 {
            client()
            sourceSet = sourceSets.extra
            ideName = "Client 2"

            programArguments.addAll('--username', 'PouffyDev_2')
            systemProperty('forge.enabledGameTestNamespaces', "tcompat")
        }

        // Client with GTM + Jade, EMI, AE2
        cleanClient {
            client()
            sourceSet = sourceSets.main
            ideName = "Clean Client"

            systemProperty('forge.enabledGameTestNamespaces', "tcompat")
        }

        server {
            server()
            sourceSet = sourceSets.main
            ideName = "Server"

            gameDirectory.set(file('run/server'))
            programArgument '--nogui'
            systemProperty('forge.enabledGameTestNamespaces', "tcompat")
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            sourceSet = sourceSets.test
            ideName = "Game Tests"

            gameDirectory.set(file('run/gametest'))
            systemProperty('forge.enabledGameTestNamespaces', "tcompat")
        }

        data {
            data()
            sourceSet = sourceSets.main
            ideName = "Data Generation"

            gameDirectory.set(file('run/data'))
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll('--mod', "tcompat")
            programArguments.addAll('--all')
            programArguments.addAll('--output', file('src/generated/resources/').getAbsolutePath())
            programArguments.addAll('--existing', file('src/main/resources/').getAbsolutePath())
            programArguments.addAll('--existing-mod', 'tconstruct', 'lodestone')
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = Level.INFO
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}